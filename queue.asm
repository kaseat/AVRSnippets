;==========================================================================
;						Очередь 
;==========================================================================
;	BLEN		- объем буфера 1-255 байт
;	RPTR		- указатель на ячейку чтения
;	WPTR		- указатель на ячейку записи
;==========================================================================
.equ BLEN = 0x0F
.def RPTR = r24 
.def WPTR = r25
.dseg 
	buff_dat:	.byte	BLEN
.cseg
;==========================================================================
;					очистка указателей чтения/записи
;==========================================================================
.macro CLRBF
	clr		Rptr
	clr		Wptr
.endmacro
;==========================================================================
;							Запись в буфер
;==========================================================================
.macro WRBF
	push	r16
	mov		r16, @0
	rcall	buff_wr
	pop		r16
.endmacro
;==========================================================================
;							Запись в буфер константы
;==========================================================================
.macro WRBFI
	push	r16
	ldi		r16, @0
	rcall	buff_wr
	pop		r16
.endmacro
;==========================================================================
;							Чтение из буфера
;==========================================================================
.macro RDBF
	push	r16
	rcall	buff_rd
	mov		@0, r16
	pop		r16
.endmacro


;==========================================================================
;							Запись в буфер
;==========================================================================
;	ВХОД:		r16
;	ВЫХОД:		SREG(T) 0 - успех, 1 - буфер заполнен
;	ВРЕМЯ:		13 циклов (rcall+ret 20 циклов)
;	РАЗМЕР:		13*2 = 26 байт
;==========================================================================
buff_wr:	set
			mov		XL, Wptr				; ставим указатель на ячейку записи
			subi	XL, low(-buff_dat)
			sbci	XH, high(-buff_dat)
			st		X, r16
			inc		Wptr					; увеличиваем указатель записи
			cpi		Wptr, BLen				; дошли до границы буфера?
			brne	PC+2
			clr		Wptr					; да! очищаем указатель записи
			cp		Wptr, Rptr				; нет! дошли до указателя чтения?
			breq	PC+2
			clt								; да! ставим флаг переполнения
			ret								; нет! выходим


;==========================================================================
;							Чтение из буфера
;==========================================================================
;	ВХОД:		N/A
;	ВЫХОД:		r16, SREG(T) 0 - успех, 1 - буфер пуст
;	ВРЕМЯ:		13 циклов (rcall+ret 20 циклов) если буфер не пуст
;				4 цикла (rcall+ret 11 циклов) если буфер пуст
;	РАЗМЕР:		13*2 = 26 байт
;==========================================================================
buff_rd:	set
			cp		Wptr, Rptr				; дошли до указателя записи?
			breq	PC+0x0A					; да! чтитать нечего, выходим						
			mov		XL, Rptr				; нет! начинаем чтение
			subi	XL, low(-buff_dat)		; ставим указатель на ячейку чтения
			sbci	XH, high(-buff_dat)
			ld		r16, X
			inc		Rptr
			cpi		Wptr, BLen				; дошли до границы буфера?
			brne	PC+2
			clr		Rptr					; да! очищаем указатель чтения
			clt								; нет! ставим флаг успешного чтения
			ret								; и выходим